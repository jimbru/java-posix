subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = 'com.jimbru.posix'
    version = '0.1.0'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
    }
}

project(':posix') {

    dependencies {
        compile 'net.java.dev.jna:jna:4.1.0'
        testCompile 'junit:junit:4.12'
    }

    test {
        dependsOn { ':testharness:fatJar' }

        testLogging {
            exceptionFormat = 'full'
            showStandardStreams = true
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'java-posix'
                    packaging 'jar'
                    description 'Easily call standard POSIX functions from Java.'
                    url 'https://github.com/jimbru/java-posix'

                    scm {
                        url 'scm:git@github.com:jimbru/java-posix.git'
                        connection 'scm:git@github.com:jimbru/java-posix.git'
                        developerConnection 'scm:git@github.com:jimbru/java-posix.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License'
                            url 'https://github.com/jimbru/java-posix/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'jimbru'
                            name 'Jim Brusstar'
                            email 'jim.brusstar@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

project(':testharness') {

    dependencies {
        compile project(':posix')
    }

    def mainClass = 'com.jimbru.posix.TestHarness'

    jar {
        manifest {
            attributes 'Main-Class': mainClass
        }
    }

    task fatJar(type: Jar) {
        manifest {
            attributes 'Implementation-Title': 'Posix TestHarness',
                       'Implementation-Version': version,
                       'Main-Class': mainClass
        }
        baseName = project.name + '-all'
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
    }
}
